plugins {
    id 'java'
    id 'org.springframework.boot' version '3.5.10'
    id 'io.spring.dependency-management' version '1.1.7'
}

group = 'com.examples'
version = '0.0.1-SNAPSHOT'
description = 'demolog'

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(21)
    }
}

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}

repositories {
    mavenCentral()
}

ext {
    querydslVersion = "6.12"
    // 경로를 조금 더 직관적으로 변경 (build 폴더 안이라 안전함)
    querydslDir = layout.buildDirectory.dir("generated/querydsl").get().asFile.absolutePath
}

sourceSets {
    main {
        java {
            // 이 설정이 있어야 IntelliJ가 Q클래스를 소스 코드로 인식합니다.
            srcDirs = ['src/main/java', querydslDir]
        }
    }
}

tasks.withType(JavaCompile).configureEach {
    // 컴파일 시 Q클래스가 위에서 지정한 경로에 쌓이도록 강제
    options.getGeneratedSourceOutputDirectory().set(file(querydslDir))
}

clean {
    // 빌드 클린 시 생성된 Q클래스도 깔끔하게 삭제
    delete file(querydslDir)
}

dependencies {
    // spring boot
    implementation('org.springframework.boot:spring-boot-starter-web') { // undertow 사용을 위한 web starter에서 tomcat 제외
        exclude group: 'org.springframework.boot', module: 'spring-boot-starter-tomcat'
    }
    implementation 'org.springframework.boot:spring-boot-starter-undertow'
    implementation 'org.springframework.boot:spring-boot-starter-aop'
    implementation 'org.springframework.boot:spring-boot-starter-security'
    implementation 'org.springframework.boot:spring-boot-starter-validation'
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation 'org.springframework.boot:spring-boot-starter-actuator'
    implementation 'org.springframework.boot:spring-boot-starter-data-redis'
    implementation 'org.springframework.boot:spring-boot-starter-aop'
    implementation 'org.springframework.kafka:spring-kafka' // Kafka

    // queryDSL
    implementation "io.github.openfeign.querydsl:querydsl-jpa:${querydslVersion}"
    annotationProcessor "io.github.openfeign.querydsl:querydsl-apt:${querydslVersion}:jpa"
    annotationProcessor 'jakarta.persistence:jakarta.persistence-api'
    testAnnotationProcessor "io.github.openfeign.querydsl:querydsl-apt:${querydslVersion}:jpa"
    testAnnotationProcessor 'jakarta.persistence:jakarta.persistence-api'

    // Spring Session Redis
    implementation 'org.springframework.session:spring-session-data-redis'

    // MySQL driver
    runtimeOnly 'com.mysql:mysql-connector-j'

    // 스웨거 + (commons-lang3 취약한 버전을 덮어쓰기 위해 명시적으로 추가)
    implementation 'org.springdoc:springdoc-openapi-starter-webmvc-ui:2.8.15'
    implementation 'org.apache.commons:commons-lang3:3.18.0'

    // Lombok
    compileOnly 'org.projectlombok:lombok'
    annotationProcessor 'org.projectlombok:lombok'

    // devtools
    developmentOnly 'org.springframework.boot:spring-boot-devtools'
    developmentOnly 'org.springframework.boot:spring-boot-docker-compose'

    // test
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'org.springframework.security:spring-security-test'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'

}

tasks.named('test') {
    useJUnitPlatform()
}
